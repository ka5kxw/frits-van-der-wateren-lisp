   NAM   LISPDRV
*
* JANUARY 4, 1978
* ROUTINES TO PROVIDE DISK
* DRIVERS FOR LISP INTERPRETER WRITTEN
* BY FRITS VAN DER WATEREN
* AND FEATURED IN DR. DOBBS JOURNAL,
* NUMBER 28.
 SPC2
* BY DALE L. PUCKETT
* CHIEF PHOTOJOURNALIST
* U.S. COAST GUARD
 SPC1
* EQUATES
 SPC1
FCB   EQU   $7740
FMS   EQU   $7806
FMSCLS   EQU   $7803
WARMS   EQU   $7103
PSTRNG   EQU   $7118
SETEXT   EQU   $712D
RPTERR   EQU   $713C
WDRIVE   EQU   $708C
ARG2   EQU   $2E
TRUE   EQU   $06EB
NIL   EQU   0
   SPC1
   ORG   $3D00
   SPC1
LISPD   BRA   OPNRED
VN   FCB   1
RDORWR   RMB   1
LSPNAM   RMB   2
FILNAM   RMB   2
XSAV   RMB   2
   SPC1
OPNRED   PSHB   
SAVE B-REG
   LDAA   #1   OPEN FOR READ
   STAA   RDORWR   SET FLAG
   BSR   GETNAM   MOVE FILENAME
   BRA   OPNFIL   GO FINISH
   SPC1
GETNAM   LDX   ARG2   NAME IN LISP
   STX   LSPNAM   POINTER
   LDX   #FCB+4   FILENAME IN FCB
   STX   FILNAM   ANOTHER POINTER
   LDAB   #8   FILENAME COUNT
RDNAM   LDX   LSPNAM
   LDAA   0,X   GET CHARACTER
   INX   BUMP
   STX   LSPNAM   UPDATE
   LDX   FILNAM   POINT TO TARGET
   CMPA   #$29   A ')'
   BEQ   CONTIN   THATS END OF NAME
   STAA   0,X   ELSE PUT IN FCB
   INX   BUMP
   STX   FILNAM   UPDATE
   DECB   
DECREMENT COUNT
   BNE   RDNAM   LOOP TILL DONE
   BRA   FILEXT
CONTIN   CLRA   
CLEAR EXTENSION
CONT1   STAA   0,X
   INX
   DECB
   BNE   CONT1
   SPC1
FILEXT   LDX   #FCB+12   POINT TO EXTENSION
   CLR   0,X
   CLR   1,X
   CLR   2,X
   LDX   #FCB
   LDAA   WDRIVE   GET WORK DRIVE NO.
   STAA   3,X   PUT IN FCB
   LDAA   #1   SET EXTENSION AS (TXT)
   JSR   SETEXT
   RTS
   SPC1
OPNFIL   LDX   #FCB
   LDAA   RDORWR   GET FLAG
   STAA   0,X   PUT IN FCB
   JSR   FMS   DO IT
   BNE   ERROR   GO ON ERROR
   PULB   
RESTORE B-REG
   JMP   TRUE   REPORT SUCCESS TO LISP
   SPC1
ERROR   JSR   RPTERR   REPORT IT
   JSR   FMSCLS   CLOSE FILES
   LDX   #0   TELL LISP YOU FAILED
   PULB   
RESTORE B-REG
   RTS
   SPC1
DISKIN   PSHB   
SAVE B-REG
   STX   XSAV   AND X-REG
   LDX   #FCB
   JSR   FMS   GET CHAR.
   BNE   EOFCHK   GO ON ERROR
RET10   LDX   XSAV   RESTORE X-REG
   PULB   
AND B-REG
   RTS
   SPC1
EOFCHK   LDAB   1,X   GET ERROR CODE
   CMPB   #8   IS IT EOF?
   BNE   RERROR   GO IF NOT
   LDAB   #4   IF SO,
   STAB   0,X   CLOSE FILE
   JSR   FMS
   BNE   RERROR
   LDX   #MSG1   REPORT EOF ERROR
   JSR   PSTRNG
RETLSP   LDX   XSAV   RESTORE X-REG
   PULB   
AND B-REG
   LDAA   #$D   A CR IS A STANDARD
   JMP   $019C   THIS IS LISP.
   SPC1
RERROR   JSR   RPTERR   REPORT IT
   BRA   RETLSP
   SPC1
DISKOU   PSHA   SAVE A-REG
   STX   XSAV   AND X-REG
   LDX   #FCB
   JSR   FMS   SEND CHAR.
   BNE   WERROR   GO ON ERROR
WRET10   LDX   XSAV   ELSE RESTORE X
   PULA   
AND A-REG
   RTS   THEN RETURN
   SPC1
WERROR   JSR   RPTERR   GO REPORT IT
   BRA   WRET10   AND RETURN TO LISP
   SPC1
DISKCL   PSHB   
SAVE ROUTINE WHETHER
   LDX   #FCB   READING OR WRITING
   LDAA   #4   GET FUNCTION CODE
   STAA   0,X   PUT IN FCB
   JSR   FMS   AND DO IT
   BNE   ERROR
   PULB   
RESTORE B-REG
   JMP   TRUE   REPORT SUCCESS
   SPC1
OPNWRT   PSHB   
SAVE B-REG
   LDAA   #2
   STAA   RDORWR
   JSR   GETNAM
   JMP   OPNFIL
   SPC1
MSG1   FCC   'END OF FILE REACHED'
   FCB   4
   PAG
* DEVICE 1 IS TERMINAL
* ITS POINTERS REMAIN THE
* SAME. WE CHANGE THE
* POINTERS TO THE OTHER DEVICES
* IN THE FOLLOWING CODE.
   SPC1
* THE FIRST POINTER RETURNS A CHAR. IN A.
* THE SECOND POINTER TAKES A CHAR. FROM A.
* THE THIRD POINTER OPENS A FILE ON A
* FILE ORIENTED DEVICE.
* THE FOURTH CLOSES A FILE ON A FILE
* ORIENTED DEVICE.
* LOCATION $02C5 IS A RTS.
   SPC1
* DEVICE 2 FIRST
   SPC1
   ORG   $010E
   FDB   DISKIN
   FDB   $02C5
   FDB   OPNRED
   FDB   DISKCL
   SPC1
* DEVICE 3.
   SPC1
   FDB   $02C5
   FDB   DISKOU
   FDB   OPNWRT
   FDB   DISKCL
   SPC1
* DEVICE 4.
* SET TO RETURN TO FLEX
   SPC1
   FDB   $7103
   FDB   $7103
   FDB   $7103
   FDB   $7103
   SPC1
* DEVICE 5.
* SET TO RETURN TO FLEX
   SPC1
   FDB   $7103
   FDB   $7103
   FDB   $7103
   FDB   $7103
   SPC1
   ORG   $0376
   FCB   $0A
   SPC1
   ORG   $0149
   FDB   $3CFE
   SPC1
   END
